import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.gradleup.shadow' version '8.3.5'
    id 'org.cadixdev.licenser' version '0.6.1' apply false
    id 'fabric-loom' version "$fabric_loom_version" apply false
    id 'org.ajoberstar.grgit' version '5.3.0'
    id 'maven-publish'
    id 'java'
}

group 'net.william278'
version "$ext.plugin_version${versionMetadata()}"
description "$ext.plugin_description"
defaultTasks 'licenseFormat', 'build'

ext {
    set 'version', version.toString()
    set 'description', description.toString()

    set 'minecraft_version', minecraft_version.toString()
    set 'jedis_version', jedis_version.toString()
    set 'mysql_driver_version', mysql_driver_version.toString()
    set 'mariadb_driver_version', mariadb_driver_version.toString()
    set 'postgres_driver_version', postgres_driver_version.toString()
    set 'mongodb_driver_version', mongodb_driver_version.toString()
    set 'snappy_version', snappy_version.toString()
}

publishing {
    repositories {
        if (System.getenv("RELEASES_MAVEN_USERNAME") != null) {
            maven {
                name = "william278-releases"
                url = "https://repo.william278.net/releases"
                credentials {
                    username = System.getenv("RELEASES_MAVEN_USERNAME")
                    password = System.getenv("RELEASES_MAVEN_PASSWORD")
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
        if (System.getenv("SNAPSHOTS_MAVEN_USERNAME") != null) {
            maven {
                name = "william278-snapshots"
                url = "https://repo.william278.net/snapshots"
                credentials {
                    username = System.getenv("SNAPSHOTS_MAVEN_USERNAME")
                    password = System.getenv("SNAPSHOTS_MAVEN_PASSWORD")
                }
                authentication {
                    basic(BasicAuthentication)
                }
            }
        }
    }
}

allprojects {
    apply plugin: 'com.gradleup.shadow'
    apply plugin: 'org.cadixdev.licenser'
    apply plugin: 'java'

    compileJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs += ['-Xlint:unchecked', '-Xlint:deprecation']
    compileJava.options.release.set Integer.parseInt(rootProject.ext.javaVersion)
    javadoc.options.encoding = 'UTF-8'
    javadoc.options.addStringOption('Xdoclint:none', '-quiet')

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://repo.william278.net/releases/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url "https://repo.dmulloy2.net/repository/public/" }
        maven { url 'https://repo.codemc.io/repository/maven-public/' }
        maven { url 'https://repo.minebench.de/' }
        maven { url 'https://repo.alessiodp.com/releases/' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://mvn-repo.arim.space/lesser-gpl3/' }
        maven { url 'https://libraries.minecraft.net/' }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.3'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.11.3'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
    }

    test {
        useJUnitPlatform()
    }

    license {
        header = rootProject.file('HEADER')
        include '**/*.java'
        newLine = true
    }

    processResources {
        def tokenMap = rootProject.ext.properties
        tokenMap.merge("grgit",'',(s, s2) -> s)
        filesMatching(['**/*.json', '**/*.yml']) {
            filter ReplaceTokens as Class, beginToken: '${', endToken: '}',
                    tokens: tokenMap
        }
    }
}

subprojects {
    if (['fabric'].contains(project.name)) {
        apply plugin: 'fabric-loom'
    }

    version rootProject.version
    archivesBaseName = "${rootProject.name}-${project.name.capitalize()}"

    jar {
        from '../LICENSE'
    }

    shadowJar {
        destinationDirectory.set(file("$rootDir/target"))
        archiveClassifier.set('')
    }

    // Append the compatible Minecraft version to the version
    if (['bukkit', 'paper', 'fabric'].contains(project.name)) {
        version += "+mc.${minecraft_version}"
    }

    // API publishing
    if (['common', 'bukkit', 'fabric'].contains(project.name)) {
        java {
            withSourcesJar()
            withJavadocJar()
        }
        sourcesJar {
            destinationDirectory.set(file("$rootDir/target"))
        }
        javadocJar {
            destinationDirectory.set(file("$rootDir/target"))
        }
        shadowJar.dependsOn(sourcesJar, javadocJar)

        publishing {
            if (['common'].contains(project.name)) {
                publications {
                    mavenJavaCommon(MavenPublication) {
                        groupId = 'net.william278.husksync'
                        artifactId = 'husksync-common'
                        version = "$rootProject.version"
                        artifact shadowJar
                        artifact sourcesJar
                        artifact javadocJar
                    }
                }
            }

            if (['bukkit'].contains(project.name)) {
                publications {
                    mavenJavaBukkit(MavenPublication) {
                        groupId = 'net.william278.husksync'
                        artifactId = 'husksync-bukkit'
                        version = "$rootProject.version+${minecraft_version}"
                        artifact shadowJar
                        artifact sourcesJar
                        artifact javadocJar
                    }
                }
            }

            if (['fabric'].contains(project.name)) {
                publications {
                    mavenJavaFabric(MavenPublication) {
                        groupId = 'net.william278.husksync'
                        artifactId = 'husksync-fabric'
                        version = "$rootProject.version+${minecraft_version}"
                        artifact remapJar
                        artifact sourcesJar
                        artifact javadocJar
                    }
                }
            }
        }
    }

    jar.dependsOn(shadowJar)
    clean.delete "$rootDir/target"
}

logger.lifecycle("Building HuskSync ${version} by William278 for Minecraft ${minecraft_version}")

@SuppressWarnings('GrMethodMayBeStatic')
def versionMetadata() {
    // If the force-hide-version-meta environment variable is set, return ''
    if (System.getProperty('force-hide-version-meta') != null) {
        return ''
    }

    // Require grgit
    if (grgit == null) {
        return '-unknown'
    }

    // If unclean, return the last commit hash with -indev
    if (!grgit.status().clean) {
        return '-' + grgit.head().abbreviatedId  + '-indev'
    }

    // Otherwise if this matches a tag, return nothing
    def tag = grgit.tag.list().find { it.commit.id == grgit.head().id }
    if (tag != null) {
        return ''
    }
    return '-' + grgit.head().abbreviatedId
}
