plugins {
    id 'java'
    id 'net.william278.preprocessor' version '1.0'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'maven-publish'
}

dependencies {
    implementation project(path: ':common')

    implementation 'net.william278.uniform:uniform-bukkit:1.3.4'
    implementation 'net.william278.uniform:uniform-paper:1.3.4'
    implementation 'net.william278.toilet:toilet-bukkit:1.0.13'
    implementation 'net.william278:mpdbdataconverter:1.0.1'
    implementation 'net.william278:hsldataconverter:1.0'
    implementation 'net.william278:mapdataapi:2.0'
    implementation 'org.bstats:bstats-bukkit:3.1.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.4.0'
    implementation 'dev.triumphteam:triumph-gui:3.1.12'
    implementation 'space.arim.morepaperlib:morepaperlib:0.4.4'
    implementation 'de.tr7zw:item-nbt-api:2.15.1-SNAPSHOT'

    compileOnly "io.papermc.paper:paper-api:${paper_api_version}"
    compileOnly 'com.github.retrooper:packetevents-spigot:2.8.0'
    compileOnly 'com.github.dmulloy2:ProtocolLib:5.3.0'
    compileOnly 'org.projectlombok:lombok:1.18.38'
    compileOnly 'commons-io:commons-io:2.19.0'
    compileOnly 'org.json:json:20250517'
    compileOnly 'net.william278:minedown:1.8.2'
    compileOnly 'de.exlll:configlib-yaml:4.6.1'
    compileOnly 'com.zaxxer:HikariCP:6.3.0'
    compileOnly 'net.william278:DesertWell:2.0.4'
    compileOnly 'net.william278:AdvancementAPI:97a9583413'
    compileOnly "redis.clients:jedis:$jedis_version"

    annotationProcessor 'org.projectlombok:lombok:1.18.38'
}

processResources {
    filesMatching(['**/*.json', '**/*.yml']) {
        expand([
                version: version,
                paper_api_version: paper_api_version,
                minecraft_version: project.name,
                minecraft_api_version: minecraft_api_version
        ])
    }
}

sourceSets.main {
    java.srcDirs '../src/main/java'
    resources.srcDirs '../src/main/resources'
}
javadoc.setSource('./build/generated/preprocessed/main/java')

preprocess {
    vars.put('MC', minecraft_version_numeric)
}

shadowJar {
    dependencies {
        exclude(dependency('com.mojang:brigadier'))
    }

    relocate 'org.apache.commons.io', 'net.william278.husksync.libraries.commons.io'
    relocate 'org.apache.commons.text', 'net.william278.husksync.libraries.commons.text'
    relocate 'org.apache.commons.lang3', 'net.william278.husksync.libraries.commons.lang3'
    relocate 'com.google.gson', 'net.william278.husksync.libraries.gson'
    relocate 'com.fatboyindustrial', 'net.william278.husksync.libraries'
    relocate 'de.themoep', 'net.william278.husksync.libraries'
    relocate 'org.jetbrains', 'net.william278.husksync.libraries'
    relocate 'org.intellij', 'net.william278.husksync.libraries'
    relocate 'com.zaxxer', 'net.william278.husksync.libraries'
    relocate 'de.exlll', 'net.william278.husksync.libraries'
    relocate 'net.william278.uniform', 'net.william278.husksync.libraries.uniform'
    relocate 'net.william278.toilet', 'net.william278.husksync.libraries.toilet'
    relocate 'net.william278.desertwell', 'net.william278.husksync.libraries.desertwell'
    relocate 'net.william278.paginedown', 'net.william278.husksync.libraries.paginedown'
    relocate 'net.william278.mapdataapi', 'net.william278.husksync.libraries.mapdataapi'
    relocate 'net.william278.mpdbconverter', 'net.william278.husksync.libraries.mpdbconverter'
    relocate 'net.william278.hslmigrator', 'net.william278.husksync.libraries.hslconverter'
    relocate 'org.json', 'net.william278.husksync.libraries.json'
    relocate 'net.querz', 'net.william278.husksync.libraries.nbtparser'
    relocate 'net.roxeez', 'net.william278.husksync.libraries'
    relocate 'org.bstats', 'net.william278.husksync.libraries.bstats'
    relocate 'dev.triumphteam.gui', 'net.william278.husksync.libraries.triumphgui'
    relocate 'space.arim.morepaperlib', 'net.william278.husksync.libraries.paperlib'
    relocate 'de.tr7zw.changeme.nbtapi', 'net.william278.husksync.libraries.nbtapi'

    minimize()
}

tasks {
    runServer {
        minecraftVersion(project.name)

        downloadPlugins {
            github("plan-player-analytics", "Plan", "5.6.2965", "Plan-5.6-build-2965.jar")
        }
    }
}